{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa Punto2
procesos
  proceso juntarPapeles(ES papeles: numero)
  comenzar
    mientras(HayPapelEnLaEsquina)
      tomarPapel
      papeles := papeles + 1
  fin
  proceso juntarFlores(ES flores: numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      flores := flores + 1
  fin
  proceso izquierda
  comenzar
    repetir 3
      derecha
  fin
  proceso escalonFlores(E medidaAyL : numero; ES cantFlores : numero)
  comenzar
    juntarFlores(cantFlores)
    repetir medidaAyL
      mover
    juntarFlores(cantFlores)
    derecha
    repetir medidaAyL
      mover
    juntarFlores(cantFlores)
    izquierda
  fin
  proceso escalonPapeles(E medidaAyL : numero; ES cantPapeles : numero)
  comenzar
    juntarPapeles(cantPapeles)
    repetir medidaAyL
      mover
    juntarPapeles(cantPapeles)
    izquierda
    repetir medidaAyL
      mover
    juntarPapeles(cantPapeles)
    derecha
  fin
areas
  ciudad: AreaC(1,1,100,100)
robots
  robot tipo1
  variables
    quienSoy : boolean
    medida : numero
    cantFlores : numero
  comenzar
    cantFlores:= 0
    RecibirMensaje(quienSoy,robotJefe)
    medida := 5
    repetir 4
      escalonFlores(medida,cantFlores)
      medida := medida -1
    BloquearEsquina(16,16)
    EnviarMensaje(quienSoy,robotJefe)
    escalonFlores(1,cantFlores)
    izquierda
    mover
    LiberarEsquina(16,16)
    medida := 1
    repetir 4
      medida := medida + 1
      escalonFlores(medida,cantFlores)
    Informar('cantidad de flores',cantFlores)
  fin
  robot tipo2
  variables
    quienSoy : boolean
    medida : numero
    cantPapeles : numero
  comenzar 
    cantPapeles:= 0
    RecibirMensaje(quienSoy,robotJefe)
    medida := 5
    repetir 4
      escalonPapeles(medida,cantPapeles)
      medida := medida -1
    BloquearEsquina(16,16)
    escalonPapeles(1,cantPapeles)
    EnviarMensaje(quienSoy,robotJefe) 
    derecha
    mover
    LiberarEsquina(16,16)
    medida := 1
    repetir 4
      medida := medida + 1
      escalonPapeles(medida,cantPapeles)
    Informar('cantidad de papeles',cantPapeles)
  fin 
  robot jefe 
  variables 
    ganador : numero
    rGanador : numero
  comenzar
    EnviarMensaje(1,robot1)
    EnviarMensaje(2,robot2)
    RecibirMensaje(ganador,*)
    rGanador := ganador
    RecibirMensaje(ganador,*)
    Informar('ganador',rGanador)
  fin
variables
  robot1: tipo1
  robot2: tipo2
  robotJefe : jefe
comenzar
  AsignarArea(robot1,ciudad)
  AsignarArea(robot2,ciudad)
  AsignarArea(robotJefe,ciudad)
  Iniciar(robot1, 1,1)
  Iniciar(robot2, 31,1)
  Iniciar(robotJefe, 15,1)
fin