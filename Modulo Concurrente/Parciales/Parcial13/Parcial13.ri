{Bienvenidos al entorno CMRE.
Lo siguiente es un cÃƒÂ³digo de ejemplo que implementa un
proceso que recibe un nÃƒÂºmero de avenida como parÃƒÂ¡metro,
se posiciona en esa avenida y la recorre.}

programa parcial13
procesos
  proceso juntarFlores(ES flores : numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      flores := flores + 1
  fin
  proceso cuadrado(E medida : numero; ES flores : numero)
  comenzar
    repetir 4
      repetir medida
        juntarFlores(flores)
        mover
      derecha 
  fin
  proceso AvisarAlosDemas(E quienSoy : numero)
  variables
    termino : numero
  comenzar
    si(quienSoy = 1) 
      EnviarMensaje(quienSoy,r2) 
      EnviarMensaje(quienSoy,r3)
      RecibirMensaje(termino,*)
      si (termino = 2)
        RecibirMensaje(termino, r3)
      sino
        RecibirMensaje(termino, r2)
    sino
      si(quienSoy = 2)
        EnviarMensaje(quienSoy,r1) 
        EnviarMensaje(quienSoy,r3)
        RecibirMensaje(termino,*)
        si (termino = 1)
          RecibirMensaje(termino, r3)
        sino
          RecibirMensaje(termino, r1)
      sino
        EnviarMensaje(quienSoy,r1) 
        EnviarMensaje(quienSoy,r2)
        RecibirMensaje(termino,*)
        si (termino = 1)
          RecibirMensaje(termino, r2)
        sino
          RecibirMensaje(termino, r1)
  fin
areas
  area1: AreaP (1,1,7,100)
  area2: AreaP (8,1,14,100)
  area3: AreaP (15,1,21,100)
  areaJefe: AreaP (51,1,51,1)
  deposito: AreaP (50,50,50,50)
robots
  robot cuadrado
  variables 
    medida : numero
    quienSoy : numero
    cantFlores : numero
    avenida, altura : numero
    avIni,caIni : numero
  comenzar
    avenida := PosAv
    cantFlores:= 0
    RecibirMensaje(quienSoy,rJefe)
    repetir 5
      Random(medida, 1,5)
      cuadrado(medida,cantFlores)
      altura := PosCa + medida + 1
      Pos(avenida,altura)
      AvisarAlosDemas(quienSoy)
    avIni := PosAv
    caIni := PosCa
    repetir cantFlores
      BloquearEsquina(50,50)
      Pos(50,50)
      depositarFlor
      Pos(avIni,caIni)
      LiberarEsquina(50,50)
  fin
  robot jefe
  variables
    quienEs : numero
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
  fin
variables
  r1,r2,r3 : cuadrado
  rJefe : jefe
comenzar
  AsignarArea(r1, area1)
  AsignarArea(r2, area2)
  AsignarArea(r3, area3)
  AsignarArea(rJefe, areaJefe)
  AsignarArea(r1, deposito)
  AsignarArea(r2, deposito)
  AsignarArea(r3, deposito)
  Iniciar(r1, 1,1)
  Iniciar(r2, 8,1)
  Iniciar(r3, 15,1)
  Iniciar(rJefe, 51,1)
fin