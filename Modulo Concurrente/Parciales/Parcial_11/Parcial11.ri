{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos
  proceso enviarMedidas
  variables
    ancho,alto : numero
  comenzar
    Random(ancho,1,5)
    EnviarMensaje(ancho,r1)
    Random(alto,1,10)
    EnviarMensaje(alto,r1)
    Random(ancho,1,5)
    EnviarMensaje(ancho,r2)
    Random(alto,1,10)
    EnviarMensaje(alto,r2)
    Random(ancho,1,5)
    EnviarMensaje(ancho,r3)
    Random(alto,1,10)
    EnviarMensaje(alto,r3)  
  fin
  proceso juntarFlores(ES flores : numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      flores := flores + 1
  fin
  proceso depositarFlores(E flores : numero)
  variables
    avIni,caIni : numero
  comenzar
    avIni := PosAv
    caIni := PosCa
    repetir flores
      BloquearEsquina(25,25)
      Pos(25,25)
      depositarFlor
      Pos(avIni,caIni)
      LiberarEsquina(25,25)
  fin
  proceso rectangulo(E ancho : numero; E alto : numero; ES sumaFlores : numero)
  variables
    flores : numero 
  comenzar
    repetir 2
      flores := 0
      repetir alto
        juntarFlores(flores)
        mover
      sumaFlores := sumaFlores + flores
      depositarFlores(flores)
      derecha
      flores := 0
      repetir ancho
        juntarFlores(flores)
        mover
      sumaFlores := sumaFlores + flores
      depositarFlores(flores)
      derecha
  fin
  proceso calcularMaximo(ES rMax : numero; E f1: numero; E f2: numero; E f3: numero)
  variables
    fMax : numero
  comenzar
    fMax := -1
    si(f1 > fMax)
      fMax := f1
      rMax := 1
    si(f2 > fMax)
      fMax := f2
      rMax := 2
    si(f3 > fMax)
      fMax := f3
      rMax := 3
  fin
areas
  area1: AreaP (1,1,6,11)
  area2: AreaP (7,1,12,11)
  area3: AreaP (13,1,18,11)
  areaJefe: AreaP (24,24,24,24)
  deposito: AreaPC (25,25,25,25)
robots
  robot recolector
  variables
    quienSoy, ancho, alto, floresTotales : numero
  comenzar
    floresTotales := 0
    RecibirMensaje(quienSoy,rJefe)
    RecibirMensaje(ancho,rJefe)
    RecibirMensaje(alto,rJefe)
    rectangulo(ancho,alto,floresTotales)
    EnviarMensaje(quienSoy,rJefe)
    EnviarMensaje(floresTotales,rJefe)
  fin
  robot jefe
  variables
    quienEs : numero
    f1,f2,f3,rMax : numero
  comenzar
    rMax := 0
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    enviarMedidas
    repetir 3
      RecibirMensaje(quienEs,*)
      si(quienEs = 1)
        RecibirMensaje(f1,r1)
      sino
        si(quienEs = 2)
          RecibirMensaje(f2,r2)
        sino
          RecibirMensaje(f3,r3)
    calcularMaximo(rMax,f1,f2,f3)
    Informar('el que recolecto la mayor cantidad de flores es',rMax)
  fin
variables
  r1,r2,r3: recolector
  rJefe: jefe
comenzar
  AsignarArea(r1, area1)
  AsignarArea(r2, area2)
  AsignarArea(r3, area3)
  AsignarArea(rJefe, areaJefe)
  AsignarArea(r1, deposito)
  AsignarArea(r2, deposito)
  AsignarArea(r3, deposito)
  Iniciar(r1, 1,1)
  Iniciar(r2, 7,1)
  Iniciar(r3, 13,1)
  Iniciar(rJefe, 24,24)
fin