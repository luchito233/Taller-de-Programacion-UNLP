programa parcial21
procesos
  proceso juntarFlores(ES f : numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      f := f + 1
  fin
  proceso cuadrado(E medida: numero; ES esqPro : numero; ES flores : numero)
  comenzar
    repetir 4
      repetir medida
        juntarFlores(flores)
        mover
        esqPro := esqPro + 1
      derecha
  fin
  proceso depositarFlores(E f: numero)
  variables
    avIni,caIni : numero
  comenzar
    avIni := PosAv
    caIni := PosCa
    repetir f
      BloquearEsquina(50,50)
      Pos(50,50)
      depositarFlor
      Pos(avIni,caIni)
      LiberarEsquina(50,50)
  fin
  proceso calcularMaximo(ES rMax : numero; E e1: numero; E e2: numero; E e3: numero)
  variables
    eMax : numero
  comenzar
    eMax := -1
    si(e1 > eMax)
      eMax := e1
      rMax := 1
    si(e2 > eMax)
      eMax := e2
      rMax := 2
    si(e3 > eMax)
      eMax := e3
      rMax := 3
  fin
areas
  area1: AreaP (1,1,5,16)
  area2: AreaP (7,1,11,16)
  area3: AreaP (14,1,18,16)
  deposito: AreaPC (50,50,50,50)
  areaJefe: AreaP (51,51,51,51) 
robots
  robot cuadrado
  variables
    quienSoy, medida, subir: numero
    esqProcesadas, flores : numero
  comenzar
    esqProcesadas := 0
    RecibirMensaje(quienSoy,rJefe)
    repetir 3
      flores := 0
      Random(medida,1,4)
      cuadrado(medida, esqProcesadas,flores)
      depositarFlores(flores)
      subir := (PosCa + medida + 1)
      Pos(PosAv,subir)
    EnviarMensaje(quienSoy,rJefe)
    EnviarMensaje(esqProcesadas,rJefe)
  fin
  robot jefe
  variables
    e1,e2,e3: numero
    quienEs ,rMax: numero
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3) 
    e1 := 0
    e2 := 0
    e3 := 0
    rMax := 0
    repetir 3
      RecibirMensaje(quienEs,*)
      si(quienEs = 1)
        RecibirMensaje(e1,r1)
      sino
        si(quienEs = 2)
          RecibirMensaje(e2,r2)
        sino
          RecibirMensaje(e3,r3)
    calcularMaximo(rMax,e1,e2,e3)
    Informar('el r que recorrio mas esquinas es',rMax)
  fin
variables
  r1,r2,r3: cuadrado
  rJefe: jefe
comenzar
  AsignarArea(r1, area1)
  AsignarArea(r2, area2)
  AsignarArea(r3, area3)
  AsignarArea(rJefe, areaJefe)
  AsignarArea(r1, deposito)
  AsignarArea(r2, deposito)
  AsignarArea(r3, deposito)
  Iniciar(r1, 1,1)
  Iniciar(r2, 7,1)
  Iniciar(r3, 14,1)
  Iniciar(rJefe, 51,51)
fin