{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos
  proceso juntarFlores(ES flores : numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      flores := flores + 1
  fin
  proceso depositarFlores(E flores : numero)
  variables
    avIni,caIni : numero
  comenzar
    avIni := PosAv
    caIni := PosCa
    repetir flores
      BloquearEsquina(20,20)
      Pos(20,20)
      depositarFlor
      Pos(avIni,caIni)
      LiberarEsquina(20,20)
  fin
  proceso perimetro1(ES flores: numero)
  comenzar
    repetir 9
      juntarFlores(flores)
      mover
    derecha
    repetir 9
      juntarFlores(flores)
      mover
    juntarFlores(flores)
  fin
  proceso perimetro2(ES flores: numero)
  comenzar
    repetir 2 
      derecha
    repetir 9
      juntarFlores(flores)
      mover
    derecha
    repetir 9
      juntarFlores(flores)
      mover
    juntarFlores(flores)
  fin
areas
  areaA: AreaPC (1,1,11,10)
  areaB: AreaPC (13,1,23,10)
  areaJefe: AreaP (30,1,30,1)
  deposito: AreaPC (20,20,20,20)
robots
  robot tipo1
  variables
    cantFlores : numero
    quienSoy : numero
  comenzar
    cantFlores := 0
    RecibirMensaje(quienSoy,rJefe)
    perimetro1(cantFlores)
    si(quienSoy = 1)
      EnviarMensaje(cantFlores,rA2)
    sino
      EnviarMensaje(cantFlores,rB2)
    depositarFlores(cantFlores)
  fin
  robot tipo2
  variables
    sumaFlores, flores : numero
    quienSoy : numero
  comenzar
    flores := 0
    RecibirMensaje(quienSoy,rJefe)
    si(quienSoy = 2)
      RecibirMensaje(sumaFlores,rA1)
    sino
      RecibirMensaje(sumaFlores,rB1)
    perimetro2(flores)
    sumaFlores := sumaFlores + flores
    EnviarMensaje(quienSoy,rJefe)
    EnviarMensaje(sumaFlores,rJefe)
    depositarFlores(flores)   
  fin
  robot jefe
  variables
    f1,f2: numero 
    quienEs : numero
  comenzar
    EnviarMensaje(1,rA1)
    EnviarMensaje(2,rA2)
    EnviarMensaje(3,rB1)
    EnviarMensaje(4,rB2)
    repetir 2
      RecibirMensaje(quienEs,*)
      si(quienEs = 2)
        RecibirMensaje(f1,rA2)
      sino
        RecibirMensaje(f2,rB2)
    si(f1 > f2)
      Informar('el equipo A es el que junto más flores',f1)
    sino
      Informar('el equipo B es el que junto más flores',f2)
  fin
variables
  rA1,rB1 : tipo1
  rA2,rB2 : tipo2
  rJefe : jefe
comenzar
  AsignarArea(rA1, areaA)
  AsignarArea(rA2, areaA)
  AsignarArea(rB1, areaB)
  AsignarArea(rB2, areaB)
  AsignarArea(rA1, deposito)
  AsignarArea(rA2, deposito)
  AsignarArea(rB1, deposito)
  AsignarArea(rB2, deposito)
  AsignarArea(rJefe, areaJefe)
  Iniciar(rA1, 1,1)
  Iniciar(rA2, 11,10)
  Iniciar(rB1, 13,1)
  Iniciar(rB2, 23,10)
  Iniciar(rJefe, 30,1)
fin