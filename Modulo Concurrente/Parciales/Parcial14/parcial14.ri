{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos
  proceso enviarMedidas
  variables
    medida : numero
  comenzar
    Random(medida, 1, 6)
    EnviarMensaje(medida,r1)
    Random(medida, 1, 6)
    EnviarMensaje(medida,r2)
    Random(medida, 1, 6)
    EnviarMensaje(medida,r3)
  fin
  proceso juntarFlores(ES flores: numero; E tipo : boolean)
  comenzar
    si(tipo)
      mientras(HayFlorEnLaEsquina)
        tomarFlor
        flores := flores + 1
    sino
      BloquearEsquina(50,50)
      Pos(50,50)
      repetir flores
        tomarFlor
      Pos(100,100)
      LiberarEsquina(50,50)
       
  fin
  proceso depositarFlores (E flores : numero)
  variables
    avIni,caIni : numero
  comenzar
    avIni := PosAv
    caIni := PosCa
    repetir flores
      BloquearEsquina(50,50)
      Pos(50,50)
      depositarFlor
      Pos(avIni,caIni)
      LiberarEsquina(50,50)
  fin
  proceso cuadrado (E medida : numero; ES flores : numero)
  comenzar
    repetir 4
      repetir medida
        juntarFlores(flores,V)
        mover
      derecha    
  fin
  proceso calcularMaximo(ES rMax : numero; E f1 : numero; E f2 : numero; E f3 : numero )
  variables
    fMax : numero
  comenzar
    fMax := -1
    si(f1 > fMax)
      fMax := f1
      rMax := 1
    si(f2 > fMax)
      fMax := f2
      rMax := 2
    si(f3 > fMax)
      fMax := f3
      rMax := 3
  fin 
areas
  area1: AreaP (1,1,7,25)
  area2: AreaP (10,1,17,25)
  area3: AreaP (20,1,27,25)
  deposito: AreaC (50,50,50,50)
  areaJefe : AreaP(100,100,100,100)
robots
  robot recolector
  variables
    quienSoy : numero
    medida : numero
    cantFlores : numero
    subir : numero
    seguir : boolean
  comenzar
    RecibirMensaje(quienSoy,rJefe)
    RecibirMensaje(medida,rJefe)
    repetir 3
      cantFlores := 0   
      cuadrado(medida,cantFlores)
      depositarFlores(cantFlores)
      subir := PosCa + medida + 1
      Pos(PosAv,subir)
      EnviarMensaje(quienSoy,rJefe)
      EnviarMensaje(cantFlores,rJefe)
      RecibirMensaje(seguir,rJefe)
  fin
  
  robot jefe
  variables
    quienEs : numero
    flores,f1,f2,f3,rMax : numero
  comenzar
    rMax := 0
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    enviarMedidas
    repetir 9
      RecibirMensaje(quienEs, *)
      si(quienEs = 1)
        RecibirMensaje(flores,r1)
        juntarFlores(flores,F)
        f1:= f1 + flores
        EnviarMensaje(V,r1)
      sino
        si(quienEs = 2)
          RecibirMensaje(flores,r2)
          juntarFlores(flores,F)
          f2:= f2 + flores
          EnviarMensaje(V,r2)
        sino
          RecibirMensaje(flores,r3)
          juntarFlores(flores,F)
          f3:= f3 + flores
          EnviarMensaje(V,r3)
    calcularMaximo(rMax,f1,f2,f3)
    Informar('el que junto mas flores es',rMax)
  fin
variables
  r1,r2,r3: recolector
  rJefe: jefe
comenzar
  AsignarArea(r1, area1)
  AsignarArea(r2, area2)
  AsignarArea(r3, area3)
  AsignarArea(rJefe, areaJefe)
  AsignarArea(r1, deposito)
  AsignarArea(r2, deposito)
  AsignarArea(r3, deposito)
  AsignarArea(rJefe, deposito)
  Iniciar(r1, 1,1)
  Iniciar(r2, 10,1)
  Iniciar(r3, 20,1)
  Iniciar(rJefe, 100,100)
fin