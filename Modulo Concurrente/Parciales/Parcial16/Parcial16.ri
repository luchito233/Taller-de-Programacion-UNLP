programa parcial17
procesos
  proceso depositarPapeles(ES papeles : numero)
  variables
    avIni,caIni : numero
  comenzar
    avIni := PosAv
    caIni := PosCa
    repetir papeles
      BloquearEsquina(99,99)
      Pos(99,99)
      depositarPapel
      Pos(avIni,caIni)
      LiberarEsquina(99,99)
  fin
  proceso izquierda 
  comenzar
    repetir 3
      derecha
  fin
  proceso juntarPapeles(ES papeles : numero)
  comenzar
    mientras(HayPapelEnLaEsquina)
      tomarPapel
      papeles := papeles + 1
  fin
  proceso escalon(E alto: numero; E ancho : numero; E quienSoy: numero; ES esqProcesadas: numero)
  variables
    cantPapeles : numero
  comenzar
    cantPapeles := 0
    repetir alto
      si(HayPapelEnLaEsquina)
        esqProcesadas := esqProcesadas + 1
        juntarPapeles(cantPapeles)
      mover
    depositarPapeles(cantPapeles)
    derecha
    cantPapeles := 0
    repetir ancho
      si(HayPapelEnLaEsquina)
        esqProcesadas := esqProcesadas + 1
        juntarPapeles(cantPapeles)
      mover
    depositarPapeles(cantPapeles)
    izquierda
  fin
areas
  area1: AreaP (2,1,17,14)
  area2: AreaP (32,1,48,14)
  area3: AreaP (62,1,78,14)
  areaJefe: AreaP (1,1,1,1)
  deposito: AreaC (99,99,99,99)
robots
  robot papelero
  variables
    quienSoy, alto, ancho, esqProcesadas: numero
    seguir : boolean
  comenzar
    esqProcesadas := 0
    RecibirMensaje(quienSoy,rJefe)
    repetir 4
      Random(alto,1,3)
      Random(ancho,2,4)
      escalon(alto,ancho,quienSoy,esqProcesadas)
      EnviarMensaje(V,rJefe)
      RecibirMensaje(seguir,rJefe)
    EnviarMensaje(esqProcesadas,rJefe)
  fin
  robot jefe
  variables
    termino: boolean
    esqProcesadas, sumaTotal : numero
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    sumaTotal := 0
    repetir 4
      repetir 3
        RecibirMensaje(termino,*)
      EnviarMensaje(V,r1)
      EnviarMensaje(V,r2)
      EnviarMensaje(V,r3) 
    repetir 3
      RecibirMensaje(esqProcesadas,*)
      sumaTotal := sumaTotal + esqProcesadas
    BloquearEsquina(99,99)
    Pos(99,99)
    mientras(HayPapelEnLaEsquina)
      tomarPapel
    Pos(1,1)
    LiberarEsquina(99,99)
    Informar(sumaTotal)     
  fin
variables
  r1,r2,r3: papelero
  rJefe: jefe
comenzar
  AsignarArea(r1, area1)
  AsignarArea(r2, area2)
  AsignarArea(r3, area3)
  AsignarArea(rJefe, areaJefe)
  AsignarArea(r1, deposito)
  AsignarArea(r2, deposito)
  AsignarArea(r3, deposito)
  AsignarArea(rJefe, deposito)
  Iniciar(rJefe, 1,1)
  Iniciar(r1, 2,1)
  Iniciar(r2, 32,1)
  Iniciar(r3, 62,1)
fin